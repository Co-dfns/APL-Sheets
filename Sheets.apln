:Namespace Sheets

⎕IO ⎕ML←0 1

Run←{port←⍵ ⋄ r←0⍴⊂,⊂'' ⋄ h←0⍴⊂'' ⋄ ac←¯1 ¯1
 _←'Conga'⎕CY⍣(0=⊃⎕NC'Conga')⊢'conga'
 DRC←Conga.Init'SHEETS_',⍕port
 _←DRC.SetProp '.' 'EventMode' 1
 rc srv←DRC.Srv '' 'localhost' port 'http' ('Options' DRC.Options.WSAutoUpgrade)
 0≠rc:⎕SIGNAL 99
 hr←⎕NEW'HtmlRenderer'(⊂'Caption' 'APL Sheets - Copyright (c) 2025 Aaron W. Hsu')
 hr.HTML←'%PORT%'⎕R(⍕port)⊢ui_src
 0≠⊃z←DRC.Wait srv:(⍕z)⎕SIGNAL 99
 rc obj evt dat←z ⋄ evt≢'Connect':⎕SIGNAL 99
 _←DRC.SetProp srv 'Pause' 1
 0≠⊃z←DRC.Wait srv:(⍕z)⎕SIGNAL 99
 rc obj evt dat←z ⋄ evt≢'WSUpgrade':⎕SIGNAL 99
 SPEC
}

CDESEL CEDIT CSEL SHP TERM←1+⍳5

∇Z←LISTEN LC
 rc obj evt dat←{DRC.Wait'.'}⍣{0≠⊃⍺:(⍕⍺)⎕SIGNAL 99 ⋄ 'Timeout'≢2⊃⍺}⍬
 Z←LC+{
  evt≢'WSReceive':TERM			⍝ TERM: Application termination
  evt dat∘←⎕JSON ⊃dat
  evt≡'reshape':SHP			⍝ SHP: The user has changed sheet dimensions
  evt≡'select':{
	ac≡⊃(⌿⌿)'_'⎕VFI dat:CDESEL	⍝ CDESEL: Deselect a selected cell
	CSEL				⍝ CSEL: A new cell is selected
  }⍬
  evt≡'edit':CEDIT			⍝ CEDIT: Cell contents have changed
  ⎕SIGNAL 99
 }⍬
∇

∇DESELECT;cmds
 cmds←,⊂('c',∊'_',∘⍕¨ac) 'set_attr' ('class' '')
 ac←¯1 ¯1
 cmds⍪←⊂'cell' 'set_attr' ('value' '')
 cmds⍪←⊂'cell' 'set_attr' ('disabled' 'true')
 {}DRC.Send obj ((⎕JSON cmds) 1 1)
∇

∇EDIT
 (ac⊃r)←dat
 {}DRC.Send obj ((⎕JSON,⊂('c',∊'_',∘⍕¨ac) 'text'(ac⊃r)) 1 1)
∇

∇SELECT;cmds
 cmds←0⍴⊂'' '' ''
 cmds⍪←(ac∧.>¯1)⌿⊂('c',∊'_',∘⍕¨ac) 'set_attr' ('class' '')
 ac←⊃(⌿⌿)'_'⎕VFI dat
 cmds⍪←⊂('c',∊'_',∘⍕¨ac) 'set_attr' ('class' 'selected')
 cmds⍪←⊂'cell' 'set_attr' ('value' (ac⊃r))
 cmds⍪←⊂'cell' 'del_attr' 'disabled'
 cmds⍪←⊂'cell' 'focus' ''
 {}DRC.Send obj ((⎕JSON cmds) 1 1)
∇

∇RESHAPE;rows;cols;cmds;z
 rows cols←1⊃⎕VFI ⍕dat
 r←cols↑(rows↑¨r),(0⌈cols-≢r)⍴⊂rows⍴⊂''
 ac[⍸2⍴ac∨.≥rows cols]←¯1
 z←(,¨'<>&')⎕R'\&lt;' '\&gt;' '\&amp;'¨¨r
 z{'<td id="c',(∊'_',∘⍕¨⍵),'">',⍺,'</td>'}¨¨←↓⍉⍳rows cols
 z←∊,∘⍪⌿(⊂rows⍴⊂'<tr>'),z,⊂rows⍴⊂'</tr>'
 cmds←,⊂'rel' 'html' z
 cmds⍪←(ac∧.>¯1)⌿⊂('c',∊'_',∘⍕¨ac) 'set_attr' ('class' 'selected')
 {}DRC.Send obj ((⎕JSON cmds) 1 1)
∇

∇Z←SPEC
 ∆_:→LISTEN ⎕LC
 ∆_CDESEL:	∘∘∘		⋄ →0
 ∆_CEDIT:	∘∘∘		⋄ →0
 ∆_CSEL:	SELECT		⋄ →∆_CSEL_
 ∆_SHP:		RESHAPE		⋄ →∆_
 ∆_TERM:			⋄ →0

 ∆_CSEL_:→LISTEN ⎕LC
 ∆_CSEL_CDESEL:	DESELECT	⋄ →∆_
 ∆_CSEL_CEDIT:	EDIT		⋄ →∆_CSEL_
 ∆_CSEL_CSEL:	SELECT		⋄ →∆_CSEL_
 ∆_CSEL_SHP:	RESHAPE		⋄ →∆_CSEL_
 ∆_CSEL_TERM:			⋄ →0
∇

ui_src←'<!DOCTYPE html>'
ui_src⍪←'<style>'
ui_src⍪←'table#rel { margin-top: 1em; }'
ui_src⍪←'table#rel, table#rel td {'
ui_src⍪←'	border: 1px solid black;'
ui_src⍪←'	height: 1em;'
ui_src⍪←'	min-width: 6em;'
ui_src⍪←'	max-width: 12em;'
ui_src⍪←'	border-collapse: collapse;'
ui_src⍪←'}'
ui_src⍪←'#cell {'
ui_src⍪←'	width: 100%;'
ui_src⍪←'	margin-top: 0.5em;'
ui_src⍪←'	margin-bottom: 0.5em;'
ui_src⍪←'}'
ui_src⍪←'td.selected {'
ui_src⍪←'	background-color: aqua;'
ui_src⍪←'}'
ui_src⍪←'</style>'
ui_src⍪←'<label for="rows">Rows:</label>'
ui_src⍪←'<input type="number" id="rows" name="rows" min="0" value="0">'
ui_src⍪←'<label for="cols">Columns:</label>'
ui_src⍪←'<input type="number" id="cols" name="cols" min="0" value="0">'
ui_src⍪←'<input type="text" id="cell" name="cell" disabled>'
ui_src⍪←'<table id="rel"></table>'
ui_src⍪←'<script>'
ui_src⍪←'var s;'
ui_src⍪←''
ui_src⍪←'function init() {'
ui_src⍪←'	s = new WebSocket("ws://localhost:%PORT%/");'
ui_src⍪←'	s.onmessage = handle_message;'
ui_src⍪←'	rows.onchange = reshape;'
ui_src⍪←'	cols.onchange = reshape;'
ui_src⍪←'	cell.onchange = edit;'
ui_src⍪←'	rel.onclick = select;'
ui_src⍪←'}'
ui_src⍪←''
ui_src⍪←'function handle_message(evt) {'
ui_src⍪←'	var cmds = JSON.parse(evt.data);'
ui_src⍪←''
ui_src⍪←'	for (let i = 0; i < cmds.length; i++) {'
ui_src⍪←'		let [id, cmd, dat] = cmds[i];'
ui_src⍪←''
ui_src⍪←'		let node = document.getElementById(id);'
ui_src⍪←''
ui_src⍪←'		switch (cmd) {'
ui_src⍪←'		case "html":'
ui_src⍪←'			node.innerHTML = dat;'
ui_src⍪←'			break;'
ui_src⍪←'		case "text":'
ui_src⍪←'			node.innerText = dat;'
ui_src⍪←'			break;'
ui_src⍪←'		case "set_attr":'
ui_src⍪←'			node.setAttribute(...dat);'
ui_src⍪←'			break;'
ui_src⍪←'		case "del_attr":'
ui_src⍪←'			node.removeAttribute(dat);'
ui_src⍪←'			break;'
ui_src⍪←'		case "focus":'
ui_src⍪←'			node.focus();'
ui_src⍪←'			break;'
ui_src⍪←'		}'
ui_src⍪←'	}'
ui_src⍪←'}'
ui_src⍪←''
ui_src⍪←'function reshape(evt) {'
ui_src⍪←'	s.send(JSON.stringify(["reshape", [rows.value, cols.value]]));'
ui_src⍪←'}'
ui_src⍪←''
ui_src⍪←'function edit(evt) {'
ui_src⍪←'	s.send(JSON.stringify(["edit", cell.value]));'
ui_src⍪←'}'
ui_src⍪←''
ui_src⍪←'function select(evt) {'
ui_src⍪←'	s.send(JSON.stringify(["select", evt.target.id]));'
ui_src⍪←'}'
ui_src⍪←''
ui_src⍪←'window.onload = init;'
ui_src⍪←'</script>'

⍝ Persistence
⍝ Remote Server

:EndNamespace